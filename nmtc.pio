;;;----------------------------------------------------------------------------------------------------------------------
;;; timing 
;;;
;;; delays based on pio clock of 31250000 (125000000 / 4)

.define TSetup    4		; 128ns (min 60ns=tsp1, RS RW setup before E on)
.define TEnOn     32           	; 512ns (min 230ns=tpw, E on)
.define TEnOnRead 24        	; 256ns (min 120ns=td, read DB after E on)
.define TEnOff    32        	; 512ns (min 270ns=tc-tpw, min tc=500ns, E off)

;;; ---------------------------------------------------------------------------------------------------------------------
;;; nmtc_pio_start initialize the chip
;;;
;;; output only. caller must set preset pindirs.

.program nmtc_pio_start

.wrap_target	
start_loop:
	pull block				; load delay from TX_FIFO 
	out y, 32				; delay 
start_predelay:				 	; "	
	jmp y-- start_predelay		 	; "
	pull block				; load pindirs and pins from TX_FIFO
	out pindirs, 10				; set pindirs
	out pins, 10		[TSetup - 1]	; set pins
	set pins, 1		[TEnOn - 1]	; set EN pin
	set pins, 0		[TEnOff - 8] 	; clear EN pin
	out y, 1  			      	; if OSR[31] clear, continue in start mode
	jmp !y start_loop			; "
	irq set 0				; else set IRQ 0
.wrap

;;; ---------------------------------------------------------------------------------------------------------------------
;;; nmtc_pio_run normal running after initialization
;;;
;;; entry point is .wrap_target

.program nmtc_pio_run

run_busy_loop:
	;; jump all the way here, even if redoing instructions, to save an instruction
	set pins, 0		[TEnOff - 3]    ; EN=0 
.wrap_target
	mov osr, x				; load OSR with busy read command from X
	out pindirs, 10				; OSR[0:9] are pindirs
	out pins, 10		[TSetup - 1]	; OSR[10:19] are pin values
	set pins, 1		[TEnOn - 2] 	; EN=1
	jmp pin run_busy_loop			; loop if DB7 (busy flag) is set
	set pins, 0		[TEnOff - 3]	; EN=0
	pull block				; load OSR from TX_FIFO
	out pindirs, 10				; output pindirs OSR[0:9]
	out pins, 10        	[TSetup - 1]	; output pins OSR[10:19]
	set pins, 1		[TEnOn - 1]	; EN=1
	set pins, 0		[TEnOff - 3]	; EN=0
.wrap	
